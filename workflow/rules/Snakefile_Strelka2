"""Strelka2 module of CGR germline variant calling pipeline
"""

from snakemake.remote.GS import RemoteProvider as GSRemoteProvider

if clusterMode == "gcp":
    GS = GSRemoteProvider()


rule strelka2_AddSampleName:
    """Adding sampleName to Strelka genome and variant VCFs
    Change the Strelka2 output generic genome VCF file name to having the sample name in it.
    """
    input:
        gvcf=(
            "strelka2/calledByChrom/{chrom}/{sample}/results/variants/genome.S1.vcf.gz"
            if by_chrom
            else "strelka2/calledBySample/{sample}/results/variants/genome.S1.vcf.gz"
        ),
        gvcf_tbi=(
            "strelka2/calledByChrom/{chrom}/{sample}/results/variants/genome.S1.vcf.gz.tbi"
            if by_chrom
            else "strelka2/calledBySample/{sample}/results/variants/genome.S1.vcf.gz.tbi"
        ),
    output:
        gvcf=(
            "strelka2/calledByChrom/{chrom}/{sample}/results/variants/{sample}.S1.vcf.gz"
            if by_chrom
            else "strelka2/called/genome.{sample}.vcf.gz"
        ),
        gvcf_tbi=(
            "strelka2/calledByChrom/{chrom}/{sample}/results/variants/{sample}.S1.vcf.gz.tbi"
            if by_chrom
            else "strelka2/called/genome.{sample}.vcf.gz.tbi"
        ),
    conda:
        "../envs/environment.yaml"
    shell:
        "mv {input.gvcf} {output.gvcf};"
        "mv {input.gvcf_tbi} {output.gvcf_tbi};"


rule strelka_bgZipBedFiles:
    input:
        bedFile="split_regions/{chrom}.bed",
    output:
        bedFileGZ="split_regions/{chrom}.bed.gz",
        bedFileGZ_IDX="split_regions/{chrom}.bed.gz.tbi",
    benchmark:
        "run_times/strelka_bgZipBedFiles/{chrom}.tsv"
    conda:
        "../envs/environment.yaml"
    shell:
        "bgzip -c {input.bedFile} >{input.bedFile}.gz; tabix -p bed {input.bedFile}.gz;"


rule strelka2_callVariant:
    input:
        ref=GS.remote(refGenome) if clusterMode == "gcp" else refGenome,
        ref_index=GS.remote(refGenome + ".fai") if clusterMode == "gcp" else refGenome + ".fai",
        bam=(
            lambda wc: GS.remote(sample2bam[wc.sample])
            if clusterMode == "gcp"
            else lambda wc: sample2bam[wc.sample]
        ),
        bai=(
            lambda wc: GS.remote(sample2bam[wc.sample] + ".bai")
            if clusterMode == "gcp"
            else lambda wc: GS.remote(sample2bam[wc.sample])
        ),
        bedFileGZ="split_regions/{chrom}.bed.gz" if by_chrom else bedFileGZ,
        bedFileGZ_index="split_regions/{chrom}.bed.gz.tbi" if by_chrom else bedFileGZ + ".tbi",
    output:
        genome_vcf=(
            "strelka2/calledByChrom/{chrom}/{sample}/results/variants/genome.S1.vcf.gz"
            if by_chrom
            else "strelka2/calledBySample/{sample}/results/variants/genome.S1.vcf.gz"
        ),
        genome_vcf_index=(
            "strelka2/calledByChrom/{chrom}/{sample}/results/variants/genome.S1.vcf.gz.tbi"
            if by_chrom
            else "strelka2/calledBySample/{sample}/results/variants/genome.S1.vcf.gz.tbi"
        ),
        variant_vcf=(
            "strelka2/calledByChrom/{chrom}/{sample}/results/variants/variants.vcf.gz"
            if by_chrom
            else "strelka2/calledBySample/{sample}/results/variants/variants.vcf.gz"
        ),
        variant_vcf_index=(
            "strelka2/calledByChrom/{chrom}/{sample}/results/variants/variants.vcf.gz.tbi"
            if by_chrom
            else "strelka2/calledBySample/{sample}/results/variants/variants.vcf.gz.tbi"
        ),
    params:
        runDir=(
            outputDir + "/strelka2/calledByChrom/{chrom}/{sample}"
            if by_chrom
            else outputDir + "/strelka2/calledBySample/{sample}"
        ),
        exome_param=exome_param,
        threads=threads,
        memGB=memGB,
    resources:
        machine_type="n1-standard-8",
    benchmark:
        "run_times/strelka_callVariantByChrom/{chrom}/{sample}.tsv" if by_chrom else "run_times/strelka2_callVariant/{sample}.tsv"
    conda:
        "../envs/strelka.yaml"
    log:
        "logs/strelka2_callVariant/strelka2_callVariant.{chrom}.{sample}.log" if by_chrom else "logs/strelka2_callVariant/strelka2_callVariant.{sample}.log",
    shell:
        "[ -d {params.runDir} ] && rm -rf {params.runDir};"
        "configureStrelkaGermlineWorkflow.py --bam {input.bam} --ref {input.ref} {params.exome_param} \
                                --callRegions {input.bedFileGZ} --runDir {params.runDir} && \
                              {params.runDir}/runWorkflow.py -m local -j {params.threads} -g {params.memGB}"


rule strelka_concatVariantsByChrom:
    input:
        vcf=expand("strelka2/genotyped/strelka2_variants_{chrom}.bcf", chrom=chromList),
        idx=expand("strelka2/genotyped/strelka2_variants_{chrom}.bcf.csi", chrom=chromList),
    output:
        temp("strelka2/genotyped/strelka2_variants.bcf"),
    params:
        temp(directory("strelka2_concat_vcfs/")),
    benchmark:
        "run_times/strelka_concatVariantsByChrom/concatVariantsByChrom.tsv"
    conda:
        "../envs/environment.yaml"
    shell:
        "bcftools concat -Ou -a {input.vcf} | bcftools sort -T {params} -Oz -o {output}"


rule strelka_indexBCFByChrom:
    input:
        bcf="strelka2/genotyped/strelka2_variants_{chrom}.bcf",
    output:
        idx="strelka2/genotyped/strelka2_variants_{chrom}.bcf.csi",
    benchmark:
        "run_times/strelka_indexBCFByChrom/{chrom}.tsv"
    conda:
        "../envs/environment.yaml"
    shell:
        "bcftools index {input.bcf}"


rule Strelka2_compress_merged_vcfs:
    """
    """
    input:
        "strelka2/genotyped/strelka2_variants.bcf",
    output:
        gz=protected("strelka2/genotyped/strelka2_variants.vcf.gz"),
        tbi=protected("strelka2/genotyped/strelka2_variants.vcf.gz.tbi"),
    benchmark:
        "run_times/strelka2_compress_merged_vcfs/strelka2_variants.tsv"
    conda:
        "../envs/environment.yaml"
    shell:
        "bcftools view --min-ac 1 {input} | bgzip -c > {output.gz}; tabix -p vcf {output.gz}"


rule Strelka2_GLmerge_gvcfs:
    """Merge Strelka gvcfs into one multi-sample vcf using the glnexus_cli
    The glnexus_cli executable consumes the Strelka genome.vcf files.
    """
    input:
        l=(
            expand(
                "strelka2/calledByChrom/{{chrom}}/{sample}/results/variants/{sample}.S1.vcf.gz",
                sample=sampleList,
            )
            if by_chrom
            else expand("strelka2/called/genome.{sample}.vcf.gz", sample=sampleList)
        ),
        m=(
            "strelka2/called/gvcfs/manifest_{chrom}.txt"
            if by_chrom
            else "strelka2/called/gvcfs/manifest.txt"
        ),
        b=GS.remote(bedFile) if clusterMode == "gcp" else bedFile,
        glnexusConfig=GS.remote(glnexusConfig) if clusterMode == "gcp" else glnexusConfig,
    output:
        temp("strelka2/genotyped/strelka2_variants_{chrom}.bcf") if by_chrom else temp(
            "strelka2/genotyped/strelka2_variants.bcf"
        ),
    params:
        workDir=(
            "Strelka2_GLmerge_gvcfs/" + "{chrom}/" + dt
            if by_chrom
            else "Strelka2_GLmerge_gvcfs/" + dt
        ),
    benchmark:
        "run_times/strelka2_GLmerge_gvcfs/{chrom}/GLmerge.tsv" if by_chrom else "run_times/strelka2_GLmerge_gvcfs/GLmerge.tsv"
    conda:
        "../envs/environment.yaml"
    shell:
        "[ -d {params.workDir} ] && rm -rf {params.workDir}; mkdir -p {params.workDir};"
        "wget https://github.com/dnanexus-rnd/GLnexus/releases/download/v1.2.7/glnexus_cli; chmod +x glnexus_cli;"
        "./glnexus_cli --config {input.glnexusConfig} --dir {params.workDir}/GLnexus.DB --bed {input.b} --list {input.m}  > {output}"


rule strelka2_create_each_sample_manifest:
    """Create list of files for GLnexus to merge
    """
    input:
        "strelka2/calledByChrom/{chrom}/{sample}/results/variants/{sample}.S1.vcf.gz" if by_chrom else "strelka2/called/genome.{sample}.vcf.gz",
    output:
        temp("strelka2/called/manifests/{chrom}/{sample}_manifest.txt") if by_chrom else temp(
            "strelka2/called/manifests/{sample}_manifest.txt"
        ),
    benchmark:
        "run_times/strelka2_create_sample_manifest/{chrom}/{sample}_manifest.tsv" if by_chrom else "run_times/strelka2_create_sample_manifest/{sample}_manifest.tsv"
    conda:
        "../envs/environment.yaml"
    shell:
        "echo {input} > {output}"


rule strelka2_create_cohort_manifest:
    input:
        expand(
            "strelka2/called/manifests/{{chrom}}/{sample}_manifest.txt", sample=sampleList
        ) if by_chrom else expand(
            "strelka2/called/manifests/{sample}_manifest.txt", sample=sampleList
        ),
    output:
        "strelka2/called/gvcfs/manifest_{chrom}.txt" if by_chrom else "strelka2/called/gvcfs/manifest.txt",
    benchmark:
        "run_times/strelka2_create_cohort_manifest/{chrom}_manifest.tsv" if by_chrom else "run_times/strelka2_create_cohort_manifest/manifest.tsv"
    params:
        manDir=(
            outputDir + "/strelka2/called/manifests/{chrom}/"
            if by_chrom
            else outputDir + "strelka2/called/manifests/"
        ),
    conda:
        "../envs/environment.yaml"
    shell:
        "for i in {params.manDir}*.txt; do cat $i >> {output}; done"
